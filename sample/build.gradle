apply from: '../dependencyVersions.gradle'

allprojects {
    group = 'io.doov'
    version = "1.0-SNAPSHOT"
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'

    dependencies {
        constraints {
            implementation group: 'io.doov', name: 'doov-core', version: '1.0-SNAPSHOT'
            implementation group: 'io.doov', name: 'doov-assertions', version: '1.0-SNAPSHOT'
            implementation group: 'io.doov', name: 'doov-assertions-core', version: '1.0-SNAPSHOT'
            implementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
            implementation group: 'javax.validation', name: 'validation-api', version: '2.0.0.Final'
            implementation group: 'com.datastax.oss', name: 'java-driver-core', version: '4.7.2'
            implementation group: 'com.datastax.oss', name: 'java-driver-query-builder', version: '4.7.2'
            implementation group: 'commons-io', name:'commons-io', version: commonsIoVersion
            implementation group: 'org.openjdk.jmh', name: 'jmh-core', version: jmhVersion
            implementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: jmhVersion
            implementation group: 'org.jfree', name: 'jfreechart', version: jfreechartVersion

            runtimeOnly group: 'org.glassfish.web', name: 'javax.el', version: javaxElVersion
            runtimeOnly group: 'javax.el', name: 'javax.el-api', version: javaxElApiVersion
            runtimeOnly group: 'org.hibernate.validator', name: 'hibernate-validator', version: hibernateValidatorVersion
        }
        implementation group: 'org.apache.commons', name: 'commons-text', version: commonsTextVersion
        implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
        testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoCoreVersion
        testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformLauncherVersion
    }

    compileJava {
        sourceCompatibility = '11'
        targetCompatibility = '11'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-proc:none'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }

    jacoco {
        toolVersion = "0.8.2"
    }

    jacocoTestReport {
        reports {
            xml
            html
        }
    }

    check.dependsOn(jacocoTestReport)
}

wrapper {
    gradleVersion = '6.8.1'
    distributionType = Wrapper.DistributionType.ALL
}