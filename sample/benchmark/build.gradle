buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.jmh:jmh-gradle-plugin:${gradleJmhPluginVersion}"
    }
}

apply plugin: "me.champeau.jmh"

dependencies {
    implementation project(':doov-sample-base')
    implementation project(':doov-sample-generated')
    implementation project(':doov-sample-example')
    implementation group: 'io.doov', name: 'doov-core'
    implementation group: 'io.doov', name: 'doov-assertions'
    implementation group: 'javax.validation', name: 'validation-api'
    implementation group: 'org.assertj', name: 'assertj-core'
    implementation group: 'org.openjdk.jmh', name: 'jmh-core'
    implementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess'
    implementation (group: 'org.jfree', name: 'jfreechart') {
        exclude group: 'javax.servlet', module: 'servlet-api'
        exclude group: 'junit', module: 'junit'
    }
    runtimeOnly group: 'org.glassfish.web', name: 'javax.el'
    runtimeOnly group: 'javax.el', name: 'javax.el-api'
    runtimeOnly group: 'org.hibernate.validator', name: 'hibernate-validator'
}

task copyEmptyBmList(type: Copy) {
    from "$buildDir/jmh-generated-classes/META-INF/BenchmarkList"
    into "$buildDir/tmp"
}
jmhRunBytecodeGenerator.finalizedBy copyEmptyBmList

task copyEmptyBmListBack(type: Copy) {
    from "$buildDir/tmp/BenchmarkList"
    into "$buildDir/jmh-generated-classes/META-INF"
}
jmhCompileGeneratedClasses.finalizedBy copyEmptyBmListBack

sourceSets {
    jmh {
        java.srcDirs "${projectDir}/src/main/java"
    }
}


jmh {
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
}

build.dependsOn jmhJar